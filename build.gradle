plugins {
    id 'com.github.johnrengelman.shadow' version '5.1.0'
    id 'java'
}

group 'com.fiskaly'
version '0.0.1-alpha'

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'signing'

    apply {
        repositories {
            mavenCentral()
        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        baseName = 'com.fiskaly.kassensichv'
        classifier 'sources'
        from sourceSets.main.allSource
    }

    task docJar(type: Jar, dependsOn: javadoc) {
        baseName = 'com.fiskaly.kassensichv'
        classifier 'javadoc'
        from javadoc
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                  authentication(userName: nexusUsername, password: nexusPassword)
                }

            }
        }
    }

    artifacts {
      archives docJar, sourcesJar
    }

    signing {
      sign configurations.archives
    }
}

repositories {
    mavenCentral()
}

def target = System.properties.getProperty("target")

dependencies {
    if (target == "general") {
        implementation project(':platform-general')
    }

    if (target == "android") {
        implementation project(':platform-android')
    }

    implementation project(':platform-common')
    implementation project(':client')
}

shadowJar {
    baseName = "com.fiskaly.kassensichv.client-" + target
}
