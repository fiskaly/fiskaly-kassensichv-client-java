plugins {
    id 'com.github.johnrengelman.shadow' version '5.1.0'
    id 'java'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'signing'

    group 'com.fiskaly.kassensichv'
    version '0.0.1-alpha'

    apply {
        repositories {
            mavenCentral()
        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        baseName = 'com.fiskaly.kassensichv'
        classifier 'sources'
        from sourceSets.main.allSource
    }

    task docJar(type: Jar, dependsOn: javadoc) {
        baseName = 'com.fiskaly.kassensichv'
        classifier 'javadoc'
        from javadoc
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                if (project.hasProperty("nexusUsername") && project.hasProperty("nexusPassword")) {
                    repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                        authentication(userName: nexusUsername, password: nexusPassword)
                    }
                }

                pom.project {
                    name 'fiskaly KassensichV Java Client'
                    packaging 'jar'
                    description 'Official Java client for fiskaly\'s KassensichV API'
                    url 'https://github.com/fiskaly/fiskaly-kassensichv-client-java'

                    scm {
                        connection 'scm:https://github.com/fiskaly/fiskaly-kassensichv-client-java.git'
                        developerConnection 'scm:git:https://github.com/fiskaly/fiskaly-kassensichv-client-java.git'
                        url 'https://github.com/fiskaly/fiskaly-kassensichv-client-java'
                    }

                    licenses {
                        license {
                            name 'The MIT License'
                            url 'https://github.com/fiskaly/fiskaly-kassensichv-client-java/blob/master/license.txt'
                        }
                    }

                    developers {
                        developer {
                            id 'benjamin.auinger.fiskaly'
                            name 'Benjamin Auinger'
                            email 'benjamin@fiskaly.com'
                        }
                    }
                }
            }
        }
    }

    artifacts {
      archives docJar, sourcesJar
    }

    signing {
      sign configurations.archives
    }
}

repositories {
    mavenCentral()
}

final PLATFORM_GENERAL = "general"
final PLATFORM_ANDROID = "android"

def target = System.properties.getProperty("target")

if (target == null) {
    target = PLATFORM_GENERAL
    System.out.println("No platform specified. Defaulting to 'general' ...")
}

dependencies {
    if (target == PLATFORM_GENERAL) {
        implementation project(':platform-general')
    }

    if (target == PLATFORM_ANDROID) {
        implementation project(':platform-android')
    }

    implementation project(':platform-common')
    implementation project(':client')
}

shadowJar {
    baseName = "com.fiskaly.kassensichv.client-" + target
}
